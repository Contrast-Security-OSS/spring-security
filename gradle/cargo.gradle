buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:1.5.2'
    }
}

apply plugin: org.gradle.api.plugins.cargo.CargoPlugin

cargo {
    containerId = 'tomcat7x'

    local {
        homeDir = file(project.tomcat_home)

        // If app is contrast enabled, configure contrast
        println("Checking if Contrast Enabled")
        if (project.contrast_enable) {
            def contrastAgent = new File(System.getProperty('java.io.tmpdir') + '/contrast.jar')
            println(contrastAgent.absolutePath)
            if (contrastAgent.exists()) {
                println('Using Contrast Agent at: ' + contrastAgent.getAbsolutePath())
                jvmArgs = '-javaagent:' + contrastAgent.absolutePath + '=' + project.contrast_config
            }
        }
    }
}

task integrationTomcatRun(type: org.gradle.api.plugins.cargo.tasks.local.CargoStartLocal) {
    onlyIf { !sourceSets.integrationTest.allSource.empty }
}

task integrationTomcatStop(type: org.gradle.api.plugins.cargo.tasks.local.CargoStopLocal) {
    onlyIf { !sourceSets.integrationTest.allSource.empty }
}

integrationTest {
    dependsOn integrationTomcatRun
    doFirst {
        // TODO: The port should be dynamic, like in the Tomcat Integration Tests
        def host = 'localhost:8080'
        // TODO: Need to retrieve the context path from Cargo, somehow - not quite sure how to get at this
        systemProperties['geb.build.baseUrl'] = 'http://'+host+'/sample-4.0.0.CI-SNAPSHOT/'
        systemProperties['geb.build.reportsDir'] = 'build/geb-reports'

    }

    finalizedBy integrationTomcatStop
}